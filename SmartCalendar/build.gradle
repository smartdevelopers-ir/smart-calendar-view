plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        setProperty("archivesBaseName","smart-calendar-view")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
group = 'ir.smartdevelopers'
version = '1.0.0'

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
task javadocJar2(type: Jar) {
    archiveClassifier.set( 'javadoc')
    from android.sourceSets.main.java.srcDirs
}
dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.fragment:fragment:1.2.5"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

}


afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'ir.smartdevelopers'
                artifactId = 'smart-calendar-view'
//                version = version
                pom {
                    name = 'smart-calendar-view'
                    description = 'Create table in android easily'
                    url = 'https://github.com/smartdevelopers-ir/smart-calendar-view'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'smartdevelopers'
                            name = 'mostafa babaei'
                            email = 'info@smartdevelopers.ir'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/smartdevelopers-ir/smart-calendar-view.git'
                        developerConnection = 'scm:git:ssh://github.com/smartdevelopers-ir/smart-calendar-view.git'
                        url = 'http://github.com/smartdevelopers-ir/smart-calendar-view'
                    }
                }
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'ir.smartdevelopers'
                artifactId = 'smart-calendar-view-debug'
//                version = '1.6'
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}





signing {
    sign configurations.archives
}

artifacts {
    archives sourcesJar
    archives javadocJar2
}